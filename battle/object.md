# ãƒãƒˆãƒ«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆ

ãƒãƒˆãƒ«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯ã€ã‚¦ã‚¤ãƒ«ã‚¹ã‚„ãƒŠãƒ“ã€ãƒãƒƒãƒ—ãŒå‡ºã™ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãªã©æ§˜ã€…ãªãƒãƒˆãƒ«ä¸­ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®ä¾¿å®œçš„ãªå‘¼ç§°ã§ã™ã€‚

ã“ã“ã§ã¯ãƒãƒˆãƒ«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®æ§‹é€ ã¨ã€ãã‚Œä»¥å¤–ã®è¡çªãƒ‡ãƒ¼ã‚¿ã‚„AIãƒ‡ãƒ¼ã‚¿ãªã©ã®ãƒ‡ãƒ¼ã‚¿ã«ã¤ã„ã¦èª¬æ˜ã—ã¾ã™ã€‚

ã“ã‚Œã§å¤§ä½“ã®ä¸»è¦ãªæ©Ÿèƒ½ã¯æƒã£ã¦ã„ã¾ã™ãŒã€ã¾ã ã¾ã ä¸æ˜ãªç‚¹ã‚‚ã‚ã‚Šã¾ã™ã€‚ ã„ãšã‚Œã«ã—ã¦ã‚‚ã€ã“ã®æƒ…å ±ã‚’ä½¿ã£ã¦ã‹ãªã‚Šè‡ªç”±ã«ãƒãƒˆãƒ«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒæ›¸ã‘ã‚‹ã‚ˆã†ã«ãªã‚‹ã¯ãšã§ã™ã€‚

## ğŸ’¥ ãƒãƒˆãƒ«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆ

ã¾ãšã€ãƒãƒˆãƒ«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãã®ã‚‚ã®ã§ã™ã€‚ ã“ã‚Œã«ã¯ã‚¿ã‚¤ãƒ—1,3,4ãŒã‚ã‚Šã¾ã™ã€‚ ã‚¿ã‚¤ãƒ—0ã¨2ã¯ç„¡åŠ¹ãªã‚¿ã‚¤ãƒ—ã§ã€ã‚¨ã‚°ã‚¼6ã«ã¯ã“ã‚Œã‚‰ã®ãŸã‚ã®ãƒ¡ãƒ¢ãƒªé ˜åŸŸãŒãã‚‚ãã‚‚ã‚ã‚Šã¾ã›ã‚“ã€‚

ã‚¿ã‚¤ãƒ—1ã¨4ã¯ã€ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§ã‚¿ã‚¤ãƒ ã‚¹ãƒˆãƒƒãƒ—ãƒ“ãƒƒãƒˆãŒãƒã‚§ãƒƒã‚¯ã•ã‚Œã¦ã„ã‚‹ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã§ã™ã€‚ã¤ã¾ã‚Šæš—è»¢ç³»ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã§ã™ã€‚

ã‚¦ã‚¤ãƒ«ã‚¹ã¨ãƒŠãƒ“ã¯ã‚¿ã‚¤ãƒ—1ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã§ã™ã€‚ ã‚¿ã‚¤ãƒ—4ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯ä¸€èˆ¬çš„ã«æš—è»¢ãƒãƒƒãƒ—ã«ä½¿ç”¨ã•ã‚Œã¾ã™ã€‚ã‚¿ã‚¤ãƒ—3ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯ã€ã‚¦ã‚¤ãƒ«ã‚¹ãƒ»ãƒŠãƒ“ãƒ»ãƒãƒƒãƒ—ãªã©å…¨ã¦ã®ã‚‚ã®ãŒæ”»æ’ƒã«ä½¿ç”¨ã™ã‚‹æ±ç”¨çš„ãªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã§ã™ã€‚

### ãƒ‡ãƒ¼ã‚¿æ§‹é€ 

```
00h Object flags
    Bit 0: active
    Bit 1: visable
    Bit 2: updates during pause
    Bit 3: Hide/Dont update sprite
    Bit 4: updates during timestop
    Bit 5:
    Bit 6:
    Bit 7:
01h Object index
02h Object type/sprite offset
    0x0F Object type
    0xF0 relative sprite data offset (from start of object)
03h Object memory index
04h Object parameters1
05h Object parameters2
06h Object parameters3
07h Object parameters4
08h Current state, usually
    0 - Initialize
    4 - Update
    8 - Destroy
09h Current "attack"/action (typically)
0Ah Current phase of "attack"/action (typically)
0Bh Indicates if current phase is initialized (typically)
0Ch 
0Dh 
0Eh Element
0Fh 
10h Current animation
11h Current animation(copy)
12h Panel X
13h Panel Y
14h Future Panel X
15h Future Panel Y
16h Alliance
    0 - friend
    1 - enemy
17h Direction Flip
18h Prevent animation
19h 
1ah Chips held
1bh 
1ch 
1dh 
1eh 
1fh 
20h Timer (typicaly)
22h Secondary Timer (typically)
24h Curren thp
26h Max hp
28h Name id
2Ah Chip
2Ch Damage
    8000 double damage
    4000 paralyze
    2000 Uninstall
    1000 KillerCross Skull hit
    0800 nothing
    07FF damage
2Eh Stamina damage/counter disabler
    7F stamina damage
    80 disable counter
34h Xposition
38h Yposition
3Ch Zposition
40h X velocity
44h Y velocity
48h Z velocity
4Ch Pointer to parent / pointer to childobject1
50h Pointer to childobject2
54h Collision data
58h AI Data
5Ch ?
60h-7Ch - free space for object specific variables (type 4)
60h-8Ch - free space for object specific variables (type 3)
60h-8Ch - free space for object specific variables (type 1)
```

ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯è¡çªãƒ‡ãƒ¼ã‚¿ã¨AIãƒ‡ãƒ¼ã‚¿ã‚’æŒã¤ã“ã¨ãŒã§ãã¾ã™ã€‚

ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®XYZåº§æ¨™ã¨XYZé€Ÿåº¦ã¯(16bit.16bit)ã®å›ºå®šå°æ•°ç‚¹æ•´æ•°ã§ã™ã€‚ ä¸Šä½16ãƒ“ãƒƒãƒˆãŒæ•´æ•°éƒ¨åˆ†ã§ã€ä¸‹ä½16ãƒ“ãƒƒãƒˆãŒå°æ•°éƒ¨åˆ†ã«ãªã£ã¦ã„ã¾ã™ã€‚

ã¾ãŸã€X,Y,Zåº§æ¨™ã¯ã€ãƒ•ã‚£ãƒ¼ãƒ«ãƒ‰ã®ä¸­å¿ƒã‹ã‚‰ã®ç›¸å¯¾åº§æ¨™ã§ã™ã€‚

### è¡çªãƒ‡ãƒ¼ã‚¿

è¡çªãƒ‡ãƒ¼ã‚¿ã®ã‚µã‚¤ã‚ºã¯ 168ãƒã‚¤ãƒˆ ã§ã™ã€‚

```
00h Enabled/Disabled?
01h Collision Reigon
02h Primary Element
03h ?
04h Alliance
05h Flip
06h
07h stamina damage/counter disabler
08h poison panel timer
09h hit effect
0Ah PanelX
0Bh PanelY
0Ch Direction
0Dh Counter Timer
0Eh Hit Modifier base
0Fh Hit Modifier final
10h Status Effect base
11h Status Effect final
12h Bugs
14h
18h Secondary element Weakness
19h Secondary element
24h
26h
2Eh self damage
30h self collision type flags
34h target collision type flags
38h parent object
3Ch object flags
    00000001 Guard
    00000002 ?
    00000004 ?
    00000008 invulnerable
    00000010 airshoe
    00000020 floatshoe
    00000040 currently moving
    00000080 ?
    00000100 ?
    00000200 ?
    00000400 ?
    00000800 paralyzed
    00001000 ?
    00002000 ?
    00004000 immobilized
    00008000 ?
    00010000 frozen
    00020000 superarmor
    00040000 undershirt
    00080000 currently moving
    00100000 ?
    00200000 ?
    00400000 ?
    00800000 ?
    01000000 ?
    02000000 affected by ice
    04000000 ?
    08000000 unaffected by poison
    10000000 ?
    20000000 ?
    40000000 ?
    80000000 ?
40h object flags2
44h collision index bit
54h ?
70h flags from a collision(bits from Collision Type list)
74h !! indicator
75h damage multiplier
76h damage elements
7Ch
80h final damage
82h PanelDamage1
84h PanelDamage2
86h PanelDamage3
88h PanelDamage4
8Ah PanelDamage5
8Ch PanelDamage6, used for poison
A4h Inflicted Bugs
```

### AIãƒ‡ãƒ¼ã‚¿

AIãƒ‡ãƒ¼ã‚¿ã®ã‚µã‚¤ã‚ºã¯ 256ãƒã‚¤ãƒˆ ã§ã™ã€‚

```
00h Virus/Navi indicator
    00h Virus
    01h Navi
    02h Player
01h AI index
02h 
03h 
0Eh FF
16h Version/Level
17h Version/Level
18h 
19h 
20h total damage received
22h keys held
24h keys down
26h keys up
28h previous keys held
34h anger bool
80h start of AI state (0x20 bytes)
A0h start of attack variable reigon (0x50 bytes)
```

### è¡çªã‚¿ã‚¤ãƒ—

[è¡çªã‚¿ã‚¤ãƒ—](object_collision_type.md)å‚ç…§

## ğŸ“š Important Object Functions (same for both games)

```
.definelabel sound_play, 0x080005CC
.definelabel bgmusic_play, 0x080005D4
.definelabel sprite_load_animation_data, 0x080026A4
.definelabel sprite_update, 0x080026C4
;r0 = 0x80
;r1 = sprite category
;r2 = sprite index
.definelabel sprite_load, 0x080026E4
.definelabel sprite_initialize, 0x0800272C
.definelabel sprite_decompress,0x08002B30
.definelabel sprite_set_scale_parameters,0x08002C24
.definelabel sprite_set_mosaic_scaling_parameters, 0x08002C7E
.definelabel sprite_get_mosaic_scaling_parameters, 0x08002CE0
.definelabel sprite_make_scalable, 0x08002D14
.definelabel sprite_make_unscalable, 0x08002D52
.definelabel sprite_set_palette, 0x08002D80
.definelabel sprite_get_palette, 0x08002D8C
.definelabel sprite_set_animation_alt, 0x08002D98
.definelabel sprite_set_animation, 0x08002DA4
.definelabel sprite_force_white_palette,0x08002DB0
.definelabel sprite_set_final_palette, 0x08002DB4
.definelabel sprite_get_final_palette, 0x08002DC8
.definelabel sprite_clear_final_palette, 0x08002DD8
.definelabel sprite_get_frame_parameters, 0x08002DEA
.definelabel sprite_has_shadow, 0x08002E3C
.definelabel sprite_set_color_shader, 0x08002ED0
.definelabel sprite_get_color_shader, 0x08002EDC
.definelabel sprite_set_mosaic_size, 0x08002EF6
.definelabel sprite_set_flip, 0x08002F5C
.definelabel sprite_get_flip, 0x08002F7E
.definelabel sprite_no_shadow, 0x08002F90
.definelabel sprite_set_coordinates, 0x0800307C
.definelabel sprite_add_coordinates, 0x0800308A
.definelabel sprite_get_coordinates, 0x080030A8
;r0 = object id
;r1 = xcoordinate
;r2 = ycoordinate
;r3 = zcoordinate
;r4 = parameter word
.definelabel object_spawn_type1, 0x08003320
;r0 = object id
;r1 = xcoordinate
;r2 = ycoordinate
;r3 = zcoordinate
;r4 = parameter word
.definelabel object_spawn_type3, 0x08003358
;r0 = object id
;r1 = xcoordinate
;r2 = ycoordinate
;r3 = zcoordinate
;r4 = parameter word
.definelabel object_spawn_type4, 0x080033AC
.definelabel object_free_memory, 0x08003458
.definelabel engine_set_screeneffect, 0x08006270
.definelabel engine_is_screeneffect_animating, 0x080062F8
.definelabel math_sin_table, 0x080065E0
.definelabel math_cos_table, 0x08006660
.definelabel battle_is_paused, 0x0800A03C
.definelabel battle_is_timestop, 0x0800A098
.definelabel battle_is_battle_over, 0x0800A18E
.definelabel battle_set_flags, 0x0800A2D8
.definelabel battle_clear_flags, 0x0800A2E4
.definelabel battle_get_flags, 0x0800A2F0
.definelabel battle_network_invert, 0x0800A9EC
.definelabel battle_clear_enemy_fadein_list, 0x0800A9F6
.definelabel object_timefreeze_begin, 0x0800B916
.definelabel object_dim_screen, 0x0800B94C
.definelabel object_draw_chipname, 0x0800B9B0
.definelabel object_undim_screen, 0x0800BC88
.definelabel object_timefreeze_end, 0x0800BD34
.definelabel object_get_panel_parameters, 0x0800C8F8
.definelabel object_get_panel_data_offset, 0x0800C90A
.definelabel object_update_panel_parameters, 0x0800C928
.definelabel object_crack_panel, 0x0800C938
.definelabel object_crack_panel_dup1, 0x0800C998
.definelabel object_break_panel, 0x0800C9F8
.definelabel object_break_panel_dup1, 0x0800CA34
.definelabel object_break_panel_dup2, 0x0800CA8C
.definelabel object_break_panel_dup3, 0x0800CAE8
.definelabel object_break_panel_loud, 0x0800CB44
.definelabel object_panel_set_poison, 0x0800CBA0
.definelabel object_highlight_panel, 0x0800CBD8
.definelabel object_highlight_panel_blue, 0x0800CBEE
.definelabel object_set_panel_type, 0x0800CC0A
.definelabel object_set_panel_alliance, 0x0800CC14
.definelabel object_set_panel_alliance_timer_long, 0x0800CC36
.definelabel object_set_panel_alliance_timer_short, 0x0800CC44
.definelabel object_set_panel_type_blink, 0x0800CC52
.definelabel object_is_current_panel_solid,0x0800CCA6
.definelabel object_is_panel_solid, 0x0800CCB2
.definelabel object_is_current_panel_valid, 0x0800CC66
.definelabel object_is_valid_panel, 0x0800CC72
.definelabel object_check_panel_parameters, 0x0800CC86
.definelabel object_highlight_current_collision_panels, 0x0800CCBE
.definelabel object_highlight_panel_region, 0x0800CCD4
.definelabel object_highlight_panel_region_blue, 0x0800CD20
.definelabel object_get_panels_type_alliance_count, 0x0800CDB4
.definelabel object_get_panels_type_count, 0x0800CE04
.definelabel object_hide_panel, 0x0800CE32
.definelabel object_show_panel, 0x0800CE42
.definelabel object_get_panels_except_current_filtered, 0x0800CE64
.definelabel object_get_panels_filtered, 0x0800CEA0
.definelabel object_get_random_panel_from_current_column, 0x0800CED0
.definelabel object_get_panels_in_column_ignore_row_filtered, 0x0800CF14
.definelabel object_get_panels_in_row_ignore_column_filtered, 0x0800CF42
.definelabel object_get_panels_ignore_row_filtered, 0x0800CF70
.definelabel object_get_panels_ignore_column_filtered, 0x0800CFA6
.definelabel object_get_panels_in_column_filtered, 0x0800CFDC
.definelabel object_get_panels_in_row_filtered, 0x0800D012
.definelabel object_get_enemy_player_panel_y, 0x0800D048
.definelabel object_get_enemy_player_panels, 0x0800D06A
.definelabel object_get_closest_panel_matching_row_filtered, 0x0800D086
.definelabel object_get_first_panel_in_direction_filtered, 0x0800D0BC
.definelabel object_get_first_panel_in_direction_within_distance_filtered, 0x0800D0DC
.definelabel object_get_first_panel_in_direction_filtered_dup1, 0x0800D100
.definelabel object_get_panel_reigon, 0x0800D3FE
.definelabel object_get_edge_panel_matching_row, 0x0800D4C2
.definelabel object_get_coordinates_for_panels, 0x0800E276
.definelabel object_set_coordinates_from_panels, 0x0800E29C
.definelabel object_set_panels_from_coordinates, 0x0800E2AC
.definelabel object_get_enemy_direction, 0x0800E2C0
.definelabel object_get_alliance_direction, 0x0800E2C2
.definelabel object_get_front_direction, 0x0800E2CA
.definelabel object_get_flip_direction, 0x0800E2CE
.definelabel object_subtract_hp, 0x0800E2D8
.definelabel object_add_hp, 0x0800E2EC
.definelabel object_calculate_final_damage1, 0x0800E3DE
.definelabel object_calculate_final_damage2, 0x0800E420
.definelabel object_get_flip, 0x0800E456
.definelabel object_can_move, 0x0800E5E2
.definelabel object_set_counter_time, 0x0800E9DC
.definelabel object_set_invulnerable_time, 0x0800EAFA
.definelabel object_get_enemy_by_name_range, 0x0800EBD4
.definelabel object_spawn_hiteffect, 0x0800EB9E
.definelabel object_create_ai_data, 0x0800ED2C
.definelabel enemy_get_struct1, 0x0800F214
.definelabel enemy_get_struct2, 0x0800F23C
.definelabel object_set_default_counter_time, 0x0800FDB6
.definelabel object_set_animation, 0x0800F2B6
.definelabel object_apply_damage, 0x0801162A
.definelabel object_setattack0,0x08011680
.definelabel object_setattack1,0x08011684
.definelabel object_setattack2,0x08011688
.definelabel object_setattack3,0x0801168C
.definelabel object_setattack4,0x08011690
.definelabel object_setattack5,0x08011694
.definelabel object_exit_attack_state, 0x08011714
.definelabel object_generic_destroy, 0x08016C8A
.definelabel object_create_collision_data, 0x08019892
;r0 = collision data offset
;r1 = self collision type
;r2 = target collision type
;r3 = collision modifier
.definelabel object_setup_collision_data, 0x08019FB4
.definelabel object_remove_collision_data, 0x0801A00E
.definelabel object_present_collision_data, 0x0801A018
.definelabel object_update_collision_panels, 0x0801A04C
.definelabel object_set_collision_panels_to_current, 0x0801A066
.definelabel object_clear_collision_reigon, 0x0801A074
.definelabel object_set_collision_reigon, 0x0801A07C
.definelabel object_spawn_collision_effect, 0x0801A0D4
.definelabel object_set_collision_hit_effect, 0x0801A140
.definelabel object_set_flag, 0x0801A152
.definelabel object_clear_flag, 0x0801A15C
.definelabel object_get_flag, 0x0801A166
.definelabel object_set_flag2, 0x0801A16C
.definelabel object_clear_flag2, 0x0801A176
.definelabel object_get_flag2, 0x0801A180
.definelabel object_set_collision_status_effect1, 0x0801A258
.definelabel object_set_collision_status_effect2, 0x0801A25E
.definelabel object_reserve_panel, 0x0801BB1C
.definelabel object_remove_panel_reserve, 0x0801BB46
.definelabel object_update_sprite, 0x0801BBAC
.definelabel object_update_sprite_timestop, 0x0801BBF4
.definelabel object_update_sprite_paused, 0x0801BCA6
.definelabel sprite_getflip_from_r0, 0x08022F7E
;Game Dependent Labels
;r0 =panelx
;r1 =panely
;r2 =element
;r3 =zcoordinate
;r4 =parameter word (SelfType | TargetType | hit effect| shape)
;r5 =parent object
;r6 =damage
;r7 =(????|Status effect|hit modifiers)
.definelabel spawn_collision_region, 0x080C536A
```

## âš™ï¸ è§£æã®ä¾‹1 ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰

![metalblade](../images/battle_object_ex1.gif)

ä¸Šã®ä¾‹ã¯Greiga Masteræ°ã®ãƒ‘ãƒƒãƒä¸­ã®ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã®ãƒãƒƒãƒ—ã®ã‚³ãƒ¼ãƒ‰ã‚’ä¿®æ­£ã—ãŸã‚‚ã®ã§ã™ã€‚

ã“ã®ä¾‹ã§ã¯ã€ã‚°ãƒ¬ã‚¤ã‚¬USã€ãƒ•ã‚¡ãƒ«ã‚¶ãƒ¼USã§`0x081CA740`ã‹ã‚‰å§‹ã¾ã‚‹æœªä½¿ç”¨ã®BN5ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆã«ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³ã¨ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãƒ«ãƒ¼ãƒãƒ³ã‚’æ›¸ãè¾¼ã‚€ã‚³ãƒ¼ãƒ‰ã‚’æ›¸ã„ã¦ã„ã¾ã™ã€‚

ã“ã‚Œã«ã‚ˆã‚Šã€é–¢é€£ã™ã‚‹å…¨ã¦ã®ãƒ«ãƒ¼ãƒãƒ³ã¸ã®ã‚¸ãƒ£ãƒ³ãƒ—ãŒã‚ˆã‚Šç°¡å˜ã«ãªã‚Šã¾ã™ã€‚

ã“ã®ä¾‹ã§ã¯ã€è¡çªãƒ‡ãƒ¼ã‚¿ã‚’ä½œæˆã—ã¦ç§»å‹•ã§ãã‚‹ã‚·ãƒ³ãƒ—ãƒ«ãªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æ‰±ã„ã¾ã™ã€‚

### ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³

ã¾ãšã€æ–°ã—ã„ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’ç”Ÿæˆã™ã‚‹ãŸã‚ã«ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³ã‚’ä½œæˆã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚ æ”»æ’ƒä¸­ã€r7ã¯å¸¸ã«ç¾åœ¨ã®`AttackVars`ã§ã‚ã‚Šã€r6 ã¯å¸¸ã«`AIState`ã§ã™ã€‚

```asm
metalblade_attack_mainloop:
    push {r14}
    ldr r1, [metalblade_attack_mainloop_pool]
    ldrb r0,[r7]    ; get which phase we left off on
    ldr r1,[r1,r0]
    mov r14,r15
    bx r1
    pop {r15}
    .balign 4, 0
metalblade_attack_mainloop_pool:
    .word metalblade_attack_init | 1
    .word metalblade_attack_update | 1

; initãƒ«ãƒ¼ãƒãƒ³
metalblade_attack_init:
    push {r14}
    ldrb r0, [r7,0x01]  ; BattleObject.CurAnim or BattleObject.CurAnimCopy
    cmp r0, 0x00
    bne .initialized
.init
    mov r0,0x04
    strb r0, [r7,0x01]
    ; initãƒ•ã‚§ã‚¤ã‚ºã®ã‚¿ã‚¤ãƒãƒ¼ã‚’è¨­å®š
    mov r0,0x08
    strh r0, [r7,0x10]  ; BattleObject.Timer or BattleObject.Timer2
    ; ã‚¢ãƒ‹ãƒ¡ãƒ¼ã‚·ãƒ§ãƒ³ã®è¨­å®š+ã‚«ã‚¦ãƒ³ã‚¿ãƒ¼æ™‚é–“ã®è¨­å®š
    mov r0,0x0c
    bl object_set_animation
    bl object_set_default_counter_time
    b .endroutine
.initialized:
    ; ã‚¿ã‚¤ãƒãƒ¼ã®ã‚«ã‚¦ãƒ³ãƒˆãŒ0ã«ãªã‚‹ã®ã‚’å¾…ã¤(ãƒ–ãƒ¬ãƒ¼ãƒ‰ã‚’æŠ•ã’ã‚‹ã®ã‚’å°‘ã—å¾…ã£ã¦ã„ã‚‹)
    ldrh r0, [r7,0x10]
    sub r0,0x01
    strh r0, [r7,0x10]
    bgt .endroutine
    mov r0,4
    strh r0,[r7]
.endroutine:
    pop {r15}

; updateãƒ«ãƒ¼ãƒãƒ³
metalblade_attack_update:
    push {r4,r5,r6,r7,r14}
    ldrb r0, [r7,0x01]
    cmp r0, 0x00
    bne .initialized
.init
    ; updateãƒ•ã‚§ã‚¤ã‚ºã®ã‚¿ã‚¤ãƒãƒ¼ã‚’è¨­å®š
    mov r0, 0x18
    strh r0, [r7,0x10]
    ; ???
    mov r0, 0x04
    strb r0, [r7,0x01]
    ; ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã‚’ç”Ÿæˆ
    bl object_get_front_direction
    ldrb r1,[r5,0x12]           ; ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ã®ç›®ã®å‰ã®ãƒ‘ãƒãƒ«
    add r0,r1,r0                ; ãƒ¡ã‚¿ãƒ–ãƒ¬ã®ãƒ‘ãƒãƒ«X(r0)
    ldrb r1,[r5,0x13]           ; ãƒ¡ã‚¿ãƒ–ãƒ¬ã®ãƒ‘ãƒãƒ«Y(r1)
    mov r2,AttackElement_Break  ; ãƒ¡ã‚¿ãƒ–ãƒ¬ã®å±æ€§(r2)
    mov r3,0x16
    lsl r3,0x10                 ; ãƒ¡ã‚¿ãƒ–ãƒ¬ã®Zåº§æ¨™(r3)
    ldr r4, [r7,0x0C]           ; metal blade parameters(r4)
    ldr r6, [r7,0x08]           ; metal blade damage(r6)
    bl spawn_metalblade
    b .endroutine
.initialized:
    ; ã‚¿ã‚¤ãƒãƒ¼ã®ã‚«ã‚¦ãƒ³ãƒˆãŒ0ã«ãªã‚‹ã®ã‚’å¾…ã¤(ã“ã®é–“ãŒæ”»æ’ƒæ™‚é–“ï¼Ÿ)
    ldrh r0, [r7,0x10]
    sub r0,r0,1
    strh r0, [r7,0x10]
    bgt .endroutine
    bl object_exit_attack_state ; æ”»æ’ƒã‚’çµ‚äº†ã™ã‚‹
.endroutine:
    pop {r4,r5,r6,r7,r15}
```

mainloopãƒ«ãƒ¼ãƒãƒ³ã¯ã€ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³ã®ç¾åœ¨ã®ãƒ•ã‚§ã‚¤ã‚ºã‚’å–å¾—ã—ã€initãƒ•ã‚§ã‚¤ã‚ºãªã‚‰initãƒ«ãƒ¼ãƒãƒ³ã€updateãƒ•ã‚§ã‚¤ã‚ºãªã‚‰updateãƒ«ãƒ¼ãƒãƒ³ã«ã‚¸ãƒ£ãƒ³ãƒ—ã—ã¾ã™ã€‚

initãƒ«ãƒ¼ãƒãƒ³ã¯ã‚¢ãƒ‹ãƒ¡ãƒ¼ã‚·ãƒ§ãƒ³ã‚’è¨­å®šã—ã€ã‚«ã‚¦ãƒ³ã‚¿ãƒ¼æ™‚é–“ã‚’è¨­å®šã—ã€ãƒ–ãƒ¬ãƒ¼ãƒ‰ã‚’æŠ•ã’ã‚‹ã®ã‚’å°‘ã—å¾…ã¡ã¾ã™ã€‚

updateãƒ«ãƒ¼ãƒãƒ³ã¯ã€ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã‚’ç”Ÿæˆã—ã€æ”»æ’ƒã‚’çµ‚äº†ã™ã‚‹å‰ã§å°‘ã—ã®é–“å¾…ã¡ã¾ã™ã€‚ã“ã®å¾…ã¡æ™‚é–“ãŒæ”»æ’ƒæ™‚é–“ã«ãªã£ã¦ã„ã‚‹ã‚ˆã†ã§ã™ï¼Ÿ

ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã‚’ç”Ÿæˆã™ã‚‹ãƒ«ãƒ¼ãƒãƒ³ã¯ä»¥ä¸‹ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚

```asm
; ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’ä½œæˆ
spawn_metalblade:
    push {r14}
    push {r0,r1,r2,r5}  ; save panelx, panely, element, and parent
    mov r0, demo_object_id
    bl object_spawn_type3
    mov r0,r5
    pop {r1,r2,r3,r5}   ; restore panelx, panely, element, and parent
    beq .endroutine
    strb r1, [r0,0x12]  ; set panelx
    strb r2, [r0,0x13]  ; set panely
    strb r3, [r0,0x0E]  ; set element
    str r5, [r0,0x4C]   ; set parent object
    str r6, [r0,0x2C]   ; set damage
    ldrh r3, [r5,0x16]
    strh r3, [r0,0x16]  ; make child object same alliance as parent
.endroutine:
    pop r15
```

ã“ã®ç”Ÿæˆãƒ«ãƒ¼ãƒãƒ³ã¯ã€å¼•æ•°ã¨ã—ã¦ `panelX`, `panelY`, `element`, `zCoordinate`, `parameter`(4byte), `parent`(è¦ªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆ), `damage` ã‚’å–ã‚Šã¾ã™ã€‚

ãŸã ã—å†…éƒ¨ã§å‘¼ã³å‡ºã—ã¦ã„ã‚‹ã€`object_spawn_type3` ã¯å¼•æ•°ã¨ã—ã¦ `objectid`, `xoordinate`, `ycoordinate`, `zcoordinate`, `parameter(4byte)` ã‚’å–ã‚‹ã“ã¨ã«æ³¨æ„ã—ã¦ãã ã•ã„ã€‚

ã“ã®ãŸã‚ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’åˆæœŸåŒ–ã—ãªãŒã‚‰ãƒ‘ãƒãƒ«ã‹ã‚‰åº§æ¨™ã‚’è¨­å®šã™ã‚‹ã“ã¨ãŒé‡è¦ã§ã™ã€‚

### ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãƒ«ãƒ¼ãƒãƒ³

ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³ã¨åŒæ§˜ã«ã€ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«ã‚‚ã€æ”»æ’ƒãŒç¾åœ¨ã©ã®ãƒ•ã‚§ãƒ¼ã‚ºã«ã‚ã‚‹ã‹ã‚’ã‚¸ãƒ£ãƒ³ãƒ—ã™ã‚‹ãŸã‚ã®ãƒ¡ã‚¤ãƒ³ãƒ«ãƒ¼ãƒ—ãŒå¿…è¦ã§ã™ã€‚

```asm
metalblade_object_mainloop:
    push r14
    ldr r1,[metalblade_object_mainloop_pool]
    ldrb r0, [r5,0x08]
    ldr r1, [r1,r0]
    mov r14,r15
    bx r1
    pop r15
    .balign 4, 0
metalblade_object_mainloop_pool:
    .word metalblade_object_init | 1
    .word metalblade_object_update | 1
    .word object_generic_destroy | 1  ; generic destructor works for most objects
```

å‡¦ç†ã®æµã‚Œã¯ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³ã®å ´åˆã¨å…¨ãåŒã˜ã§ã€mainloopãƒ«ãƒ¼ãƒãƒ³ã¯ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãƒ«ãƒ¼ãƒãƒ³ã®ç¾åœ¨ã®ãƒ•ã‚§ã‚¤ã‚ºã‚’å–å¾—ã—ã€initãƒ•ã‚§ã‚¤ã‚ºãªã‚‰initãƒ«ãƒ¼ãƒãƒ³ã€updateãƒ•ã‚§ã‚¤ã‚ºãªã‚‰updateãƒ«ãƒ¼ãƒãƒ³ã«ã‚¸ãƒ£ãƒ³ãƒ—ã—ã¾ã™ã€‚ä»Šå›ã¯ã•ã‚‰ã«destroyãƒ«ãƒ¼ãƒãƒ³ã‚‚è¿½åŠ ã•ã‚Œã¦ã„ã¾ã™ã€‚

initãƒ«ãƒ¼ãƒãƒ³ã§ã¯ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’visibleã«ã—ã€ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆã‚’ãƒ­ãƒ¼ãƒ‰ã—ã€è¡çªãƒ‡ãƒ¼ã‚¿ã‚’ä½œæˆã—ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆå›ºæœ‰ã®å¤‰æ•°ã‚’è¨­å®šã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚

```asm
metalblade_object_init:
    push {r4,r5,r6,r7,r14}

    ; BattleObject.Flagsã®Bit1(visibleãƒ•ãƒ©ã‚°)ã‚’ç«‹ã¦ã‚‹
    ldrb r0, [r5]
    mov r1, 0x02
    orr r0,r1
    strb r0, [r5]

    ; set proper coordinates for panel
    bl object_set_coordinates_from_panels

    ; ãƒ¡ã‚¿ãƒ–ãƒ¬ã®ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆã‚’ãƒ­ãƒ¼ãƒ‰
    mov r0, 0x80
    mov r1, 0x00
    mov r2, 0x0D
    bl sprite_load

    ; initialize to animation 0
    mov r0,0x00
    strh r0, [r5,0x10]

    ; initialize essential sprite stuff
    bl sprite_set_animation
    bl sprite_load_animation_data
    bl sprite_update        ; updates sprite data
    bl sprite_has_shadow    ; First oam is considered shadow, wont move with z
    bl object_get_flip
    bl sprite_set_flip      ; make sure the object is facing the right direction based on alliance

    ; setup movement velocities
    bl object_get_front_direction
    mov r1,0x08         ; x velocity is 8 pixels
    mul r0,r1           ; set direction
    lsl r0,r0,0x10      ; coordinates are shift left 16
    str r0, [r5,0x40]   ; set velocity
    mov r1,0x06         ; x velocity is 6 pixels
    lsl r1,r1,0x10
    str r1, [r5,0x44]   ; set velocity

    ; setup number of times to loop
    mov r0,0x3
    str r0, [r5,0x60]

    ; è¡çªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’ä½œæˆ
    bl object_create_collision_data
    ; object_create_collision_dataã«ã‚ˆã£ã¦è¡çªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒä½œæˆã•ã‚ŒãŸ -> .collisioninit
    tst r0,r0
    bne .collisioninit
    ; ä½œæˆã•ã‚Œãªã‹ã£ãŸ
    bl object_free_memory   ; if it wasnt destroy the object
    pop {r4,r5,r6,r7,r15}
.collisioninit:
    ; è¡çªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã‚’è¨­å®š
    mov r1,0x04     ; è‡ªèº«ã®è¡çªã‚¿ã‚¤ãƒ—
    mov r2,0x05     ; ã‚¿ãƒ¼ã‚²ãƒƒãƒˆã®è¡çªã‚¿ã‚¤ãƒ—
    mov r3,0x01     ; ãƒ’ãƒƒãƒˆã—ãŸæ™‚ã®æŒ™å‹•(ã²ã‚‹ã¿ã€ã‚¤ãƒ³ãƒ“ã‚¸ãªã©)
    bl object_setup_collision_data
    mov r0,0x0A     ; Break hit effect
    bl object_set_collision_hit_effect
    bl object_present_collision_data
    mov r0,0x8F
    bl sound_play   ; play whir sound

    ; æ¬¡ã«mainloopãŒå®Ÿè¡Œã•ã‚ŒãŸæ™‚ã¯updateãƒ«ãƒ¼ãƒãƒ³ã«
    mov r0,4
    strh r0, [r5,0x08]
    pop {r4,r5,r6,r7,r15}
```

`sprite_load`ã¯0x80ã‚’å¼•æ•°ã¨ã—ã¦å—ã‘å–ã‚Šï¼Œå¯¾è±¡ã®ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆã®ã‚«ãƒ†ã‚´ãƒªIDã¨ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã‚’å–å¾—ã—ã¾ã™ã€‚

ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆã®ã‚«ãƒ†ã‚´ãƒªIDã¨ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã¯ã€ãƒŸã‚¹ãƒ†ãƒªãƒ¼ãƒ‡ãƒ¼ã‚¿ã®ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆãƒãƒƒã‚­ãƒ³ã‚°ã§å–å¾—ã§ãã¾ã™ã€‚

ã“ã®ä¾‹ã§ã¯ã€ç£åŒ–(ãƒ•ã‚¡ãƒ«ã‚¶ãƒ¼)ã®ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆã®ç›´å¾Œã®`WhiteDot`ã‚¹ãƒ—ãƒ©ã‚¤ãƒˆã‚’ä½¿ã£ã¦ã„ã¾ã™ã€‚

è¡çªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒä½œæˆã•ã‚ŒãŸå ´åˆã¯ã€`object_setup_collision_data`ã§å„ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã‚’è¨­å®šã—ã¾ã™ã€‚è¡çªã‚¿ã‚¤ãƒ—ã¨ãƒ’ãƒƒãƒˆæ™‚ã®æŒ™å‹•ã‚’è¨­å®šã—ã¦ã„ã¾ã™ã€‚ä¸Šè¨˜ã®è¡çªã‚¿ã‚¤ãƒ—ã®ãƒªã‚¹ãƒˆã‹ã‚‰ã€ã“ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®è¡çªã‚¿ã‚¤ãƒ—ã¯å‘³æ–¹ã«ã‚ˆã‚‹æ”»æ’ƒæ‰±ã„ã«ãªã‚Šã¾ã™ã€‚ã¤ã¾ã‚Šã“ã®æ”»æ’ƒã®ã‚¿ãƒ¼ã‚²ãƒƒãƒˆã¯`Neutral Support Objects`, `Enemy Support Objects`, `Enemy`, `Enemies`, `Attack Objects` ã‚’å¯¾è±¡ã¨ã—ã¦ã„ã¾ã™ã€‚ã¾ãŸãƒ’ãƒƒãƒˆæ™‚ã®æŒ™å‹•ã¯ã€ã‚¤ãƒ³ãƒ“ã‚¸çŠ¶æ…‹ã«ãªã‚‰ãšæ€¯ã‚€ã‚ˆã†ã«ä»Šå›ã¯è¨­å®šã—ã¾ã™ã€‚

æ¬¡ã«updateãƒ«ãƒ¼ãƒãƒ³ã§ã™ã€‚ ã“ã®ãƒ«ãƒ¼ãƒãƒ³ã§ã¯è¡çªã‚’å‡¦ç†ã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚

```asm
metalblade_object_update:
    push {r14}
    bl object_remove_collision_data
    bl object_spawn_collision_effect
    ldr r0, [r5,0x54]
    ldr r1, [r0,0x70]
    tst r1,r1
    beq .nocollision
    bl clearCollisionRegion    ; ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒç„¡é™ã«ãƒ’ãƒƒãƒˆã—ãªã„ã‚ˆã†ã«è¡çªã—ãŸé ˜åŸŸã‚’ã‚¯ãƒªã‚¢
.nocollision:
    ldrh r4, [r5,0x12]
    ldrb r0, [r5,0x09]
    ldr r1, [metalblade_object_update_pool]
    ldr r0, [r1,r0]
    mov r14,r15
    bx r0
    bl object_set_panels_from_coordinates
    bl object_update_collision_panels
    bl object_present_collision_data
    ldrh r0, [r5,0x12]
    cmp r0,r4
    beq .dontupdate
    mov r0,0x01
    bl object_set_collision_reigon
.dontupdate:
    bl object_update_sprite
    pop {r15}

metalblade_object_update_pool:
    .word metalblade_object_update_initialmove | 1  ; move foward looking for targets
    .word metalblade_object_update_downmove | 1     ; move down 1 panel
    .word metalblade_object_update_leftmove | 1     ; move left 1 panel
    .word metalblade_object_update_upmove | 1       ; move up 1 panel
    .word metalblade_object_update_rightmove | 1    ; move right 1 panel
```

updateãƒ«ãƒ¼ãƒãƒ³ã®é–“ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯è¡çªãŒèµ·ããŸã‹ã©ã†ã‹ã‚’ãƒã‚§ãƒƒã‚¯ã—ã€è¡çªã—ãŸå ´åˆã¯`object_remove_collision_data`ã‚’ä½¿ç”¨ã—ã¦ãƒ‘ãƒãƒ«ã‹ã‚‰è¡çªãŒç™ºç”Ÿã—ãŸã¨ã„ã†åˆ¤å®šã‚’æ¶ˆã™å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚ã“ã‚Œã‚’å¿˜ã‚Œã¦ã—ã¾ã†ã¨ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«ã‚ˆã‚‹ãƒ‘ãƒãƒ«ã«ãƒ€ãƒ¡ãƒ¼ã‚¸ã‚’ä¸ãˆã‚‹åˆ¤å®šãŒæ®‹ã‚Šç¶šã‘ã¦ã—ã¾ã„ã¾ã™ã€‚

æ¬¡ã«ã€è¡çªãƒ‡ãƒ¼ã‚¿ã®`FlagsFromCollision`ã‚’ãƒã‚§ãƒƒã‚¯ã™ã‚‹ã“ã¨ã§ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒä½•ã‹ã«ã¶ã¤ã‹ã£ãŸã‹ã©ã†ã‹ã‚’ãƒã‚§ãƒƒã‚¯ã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚

ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒä½•ã‹ã«è¡çªã—ãŸå ´åˆã¯ã€`clearCollisionRegion`ã‚’å‘¼ã³å‡ºã—ã¦è¡çªã—ãŸé ˜åŸŸã‚’ã‚¯ãƒªã‚¢ã—ã¦ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒç„¡é™ã«ãƒ’ãƒƒãƒˆã—ãªã„ã‚ˆã†ã«ã™ã‚‹ã“ã¨ãŒé‡è¦ã§ã™ã€‚

è¡çªã—ãŸé ˜åŸŸãŒå†ã³è¨­å®šã•ã‚Œã‚‹ã¨ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®ãƒ‘ãƒãƒ«ã¯è¡çªã—ã¦ã‹ã‚‰å¤‰æ›´ã•ã‚Œã¦ã„ã¾ã™ãŒã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®ãƒ‘ãƒãƒ«ã¯ä¸€ç·’ã«æ›´æ–°ã•ã‚Œã¾ã›ã‚“ã€‚

åº§æ¨™ã‚’å¤‰æ›´ã—ãŸå¾Œã¯ã€`object_set_panels_from_coordinates`ã§ãƒ‘ãƒãƒ«ã‚’æ›´æ–°ã—ã€è¡çªãƒ‘ãƒãƒ«ã‚‚ä¸€è‡´ã™ã‚‹ã‚ˆã†ã«æ›´æ–°ã—ãªã‘ã‚Œã°ãªã‚Šã¾ã›ã‚“ã€‚

æœ€å¾Œã« `object_present_collision_data` ã‚’å‘¼ã³å‡ºã—ã¦ã€ãã®ãƒ‡ãƒ¼ã‚¿ã‚’ãƒ‘ãƒãƒ«ã«é…ç½®ã—ãªã‘ã‚Œã°ãªã‚Šã¾ã›ã‚“ã€‚

movementãƒ«ãƒ¼ãƒãƒ³ã®ã‚³ãƒ¼ãƒ‰ã¯ã€æ·»ä»˜ã®ãƒ•ãƒ«ã‚³ãƒ¼ãƒ‰ã«ã‚ã‚Šã¾ã™ã€‚

## ğŸ§² è§£æã®ä¾‹2 Magtect

![magtect](../images/battle_object_ex2.gif)

ã“ã®ä¾‹ã§ã¯ã€ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ã®å…¥åŠ›ã‚’å—ã‘ã¦å¤–éƒ¨è¡çªé ˜åŸŸã‚’ä½¿ç”¨ã™ã‚‹ã‚µãƒãƒ¼ãƒˆã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’ä½¿ç”¨ã—ãŸæ”»æ’ƒã«ã¤ã„ã¦èª¬æ˜ã—ã¾ã™ã€‚

### ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³

ã“ã®æ”»æ’ƒã®ã‚¢ã‚¿ãƒƒã‚¯ãƒ«ãƒ¼ãƒãƒ³ã®mainloopãƒ«ãƒ¼ãƒãƒ³ã¯ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã«æ¯”ã¹ã¦ã‚·ãƒ³ãƒ—ãƒ«ã§ã™ã€‚

```asm
magtect_attack_mainloop:
    push {r4,r5,r6,r7,r14}
    ldrb r0, [r7,0x01]
    cmp r0, 0x00
    bne .initialized
    mov r0,0x04
    strb r0, [r7,0x01]

    ; set timer for phase
    mov r0,0x08
    strh r0, [r7,0x10]
    mov r0,0x0c
    bl object_set_animation
    bl object_set_default_counter_time
    bl object_get_front_direction
    ldrb r1,[r5,0x12]           ; panel in front of you
    add r0,r1,r0                ; magtect panelx
    ldrb r1,[r5,0x13]           ; magtect panely
    mov r2,AttackElement_Elec   ; magtect element
    mov r3,0x00                 ; magtect z coordinate
    ldr r4, [r7,0x0C]           ; magtect parameters
    ldr r6, [r7,0x08]           ; magtect blade damage
    mov r7,0x4C
    add r7,r5,r7
    bl spawn_magtect
    str r0, [r5,0x4C]           ; store child object
    b .endroutine
.initialized:
    ; as long as megtect still exists attack continues
    ldr r0, [r5,0x4C]
    tst r0,r0
    bne .endroutine
    bl object_exit_attack_state
.endroutine:
    pop {r4,r5,r6,r7,r15}
```

å­ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®ãƒã‚¤ãƒ³ã‚¿ã‚’ 0x4C ã«æ ¼ç´ã™ã‚‹ã“ã¨ãŒé‡è¦ã§ã™ã€‚ ä¾‹ãˆã°ãƒ€ãƒ¡ãƒ¼ã‚¸ã‚’å—ã‘ãŸå¾Œãªã©ã§æ”»æ’ƒãŒçµ‚äº†ã—ãŸæ™‚ã«ã€ã“ã®ãƒã‚¤ãƒ³ã‚¿ã¯ã‚¯ãƒªã‚¢ã•ã‚Œã‚‹ã®ã§ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒæ”»æ’ƒãŒçµ‚äº†ã—ã¦ã„ãªã„ã‹ã©ã†ã‹ã‚’ãƒˆãƒ¬ãƒ¼ã‚¹ã™ã‚‹ãŸã‚ã«å¿…è¦ã§ã™ã€‚

ã¾ãŸã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒç ´å£Šã•ã‚ŒãŸå ´åˆã‚‚æ”»æ’ƒã‚’çµ‚äº†ã•ã›ã‚‹ãŸã‚ã«ä¸€åº¦ã ã‘ã“ã®ãƒã‚¤ãƒ³ã‚¿ã‚’ã‚¯ãƒªã‚¢ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚

```asm
; creates magtect object
spawn_magtect:
    push {r14}
    push {r0,r1,r2,r5}
    mov r0, demo_object_id
    bl object_spawn_type3
    mov r0,r5
    pop {r1,r2,r3,r5}
    beq .endroutine
    strb r1, [r0,0x12]
    strb r2, [r0,0x13]
    strb r3, [r0,0x0E]
    str r5, [r0,0x4C]
    str r6, [r0,0x2C]
    ldrh r3, [r5,0x16]
    strh r3, [r0,0x16]
    str r7, [r0,0x60]
.endroutine:
    pop {r15}
```

ã“ã‚Œã¯ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã®ã‚¹ãƒãƒ¼ãƒ³ãƒ«ãƒ¼ãƒãƒ³ã«ä¼¼ã¦ã„ã¾ã™ãŒã€ä»Šå›ã¯è¦ªãŒå­ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’æ ¼ç´ã™ã‚‹å ´æ‰€ãŒ r7 ã«ãªã£ã¦ã„ã¾ã™ã€‚

### ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãƒ«ãƒ¼ãƒãƒ³

```asm
magtect_object_mainloop:
    push {r14}
    ldr r1, [magtect_object_mainloop_pool]
    ldrb r0, [r5,0x08]
    ldr r1, [r1,r0]
    mov r14,r15
    bx r1
    pop {r15}
    .balign 4, 0
magtect_object_mainloop_pool:
    .word magtect_object_init | 1
    .word magtect_object_update | 1
    .word object_generic_destroy | 1    ; boilerplate destructor

magtect_object_init:
    push {r4,r5,r6,r7,r14}

    ; set visable bit (2)
    ldrb r0, [r5]
    mov r1, 0x02
    orr r0,r1
    strb r0, [r5]

    ; set proper coordinates for panel
    bl object_set_coordinates_from_panels

    ; load magtect sprite
    mov r0, 0x80
    mov r1, 0x00
    mov r2, 0x0D
    bl sprite_load

    ; initialize to animation 0
    mov r0,0x00
    strh r0, [r5,0x10]

    ; initialize essential sprite stuff
    bl sprite_set_animation
    bl sprite_load_animation_data
    bl sprite_update        ; updates sprite data
    bl sprite_has_shadow    ; Shadow should not follow main sprite accross Z
    bl object_get_flip
    bl sprite_set_flip
    bl object_create_collision_data
    tst r0,r0
    bne .collisioninit
    bl magtect_destroy
    pop {r4,r5,r6,r7,r15}
.collisioninit:
    mov r1,0x29
    mov r2,0x0D
    mov r3,0x03
    bl object_setup_collision_data
    mov r0,0x03
    bl object_set_collision_hit_effect

    ; give 40 HP
    mov r0,40
    strh r0, [r5,0x24]
    strh r0, [r5,0x26]

    ; set to update routine
    mov r0,4
    strh r0, [r5,0x08]
    pop {r4,r5,r6,r7,r15}
```

ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãƒ«ãƒ¼ãƒãƒ³ã®mainloopã¯ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã®ã‚‚ã®ã¨åŒã˜ã§ã™ã€‚ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰åŒæ§˜ã«åˆæœŸåŒ–å‡¦ç†ã‚’è¡Œã£ã¦ã„ã¾ã™ãŒã€ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰ã¨é•ã†ã®ã¯ã€ä»Šå›ã¯ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®HPãŒæ–°ãŸã«è¨­å®šã•ã‚Œã¦ã„ã‚‹ç‚¹ã§ã™ã€‚

ã‚³ãƒªã‚¸ãƒ§ãƒ³ã¯ã€è¡çªã‚¿ã‚¤ãƒ—ã‚’0x29ã€ã‚¿ãƒ¼ã‚²ãƒƒãƒˆã‚’0x0dã«è¨­å®šã—ã¦ã„ã¾ã™ã€‚ã“ã‚Œã¯ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒ`Movement Blocking(?)`ã§ã€å‘³æ–¹ã®ã‚µãƒãƒ¼ãƒˆã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã§ã‚ã‚Šã€ã‚¿ãƒ¼ã‚²ãƒƒãƒˆãŒæ•µå…¨èˆ¬(`Neutral Support Objects`, `Enemy Support Objects`)ã§ã‚ã‚‹ã“ã¨ã‚’æ„å‘³ã—ã¦ã„ã¾ã™ã€‚

ã¾ãŸã€æ•µã®å‡ºã—ãŸã‚¢ã‚¿ãƒƒã‚¯ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚„æ•µã®æ”»æ’ƒã«ã‚ˆã‚‹ã‚³ãƒªã‚¸ãƒ§ãƒ³ã‚’å—ã‘ä»˜ã‘ã‚‹ã®ã§ã€ã“ã®ãƒã‚°ãƒ†ã‚¯ãƒˆã«ã¯ãƒ€ãƒ¡ãƒ¼ã‚¸ã‚’ä¸ãˆã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚ 

è¡çªãƒ‡ãƒ¼ã‚¿ã‚’ä½œæˆã§ããªã„å ´åˆã¯ã€`object_free_memory`ã®ä»£ã‚ã‚Šã«`magtect_destroy`ã‚’ä½¿ç”¨ã—ã¦ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’ç ´å£Šã—ã¾ã™ã€‚

```asm
magtect_destroy:
    push {r14}
    bl object_clear_collision_region
    mov r0,0x08
    str r0, [r5,0x08]   ; set to destroy routine
    mov r0,0x00
    ldr r1, [r5,0x60]   ; get parents pointer to this object
    str r0, [r1]        ; clear object
    pop {r15}
```

ã“ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆãŒç ´å£Šã•ã‚Œã‚‹ã¨ã€è¡çªãƒ‡ãƒ¼ã‚¿ã‚’ã‚¯ãƒªã‚¢ã—ãªã‘ã‚Œã°ã„ã‘ã¾ã›ã‚“ã€‚ãã†ã—ãªã„ã¨ãƒ¡ã‚¿ãƒ«ãƒ–ãƒ¬ãƒ¼ãƒ‰åŒæ§˜ã«ãƒ•ã‚£ãƒ¼ãƒ«ãƒ‰ã«ãƒ€ãƒ¡ãƒ¼ã‚¸åˆ¤å®šã®ã‚ã‚‹ãƒ‘ãƒãƒ«ã‚’æ®‹ã™ã“ã¨ã«ãªã‚Šã¾ã™ã€‚

ã¾ãŸã€ã“ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯ã€æ”»æ’ƒçŠ¶æ…‹ã‹ã‚‰æŠœã‘ã‚‹ã“ã¨ã‚’çŸ¥ã‚‹ãŸã‚ã«ã€ã“ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¸ã®è¦ªå‚ç…§ã‚’ã‚¯ãƒªã‚¢ã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚

```asm
magtect_object_update:
    push {r14}
    bl object_remove_collision_data
    bl object_spawn_collision_effect
    mov r0,0x00
    bl object_apply_damage
    tst r0,r0           ; check if dead
    bne .dead
    ldr r0, [r5,0x60]   ; get parents refence to this object
    ldr r0, [r0]        ; check if its still there
    tst r0,r0
    beq .dead           ; if its cleared parent is not in attack state
    ldrb r0, [r5,0x09]
    ldr r1, [magtect_object_update_pool]
    ldr r0, [r1,r0]
    mov r14,r15
    bx r0
    bl object_update_sprite
    b .endroutine
.dead:
    bl magtect_destroy
.endroutine:
    bl object_present_collision_data
    pop {r15}
    .balign 4, 0
magtect_object_update_pool:
    .word magtect_object_update_idle | 1    ; wait for button presses
    .word magtect_object_update_pull | 1    ; pull enemy towards magtect while B pressed
    .word magtect_object_update_attack | 1  ; magnet attack
```

ãƒ€ãƒ¡ãƒ¼ã‚¸ã®ãƒã‚§ãƒƒã‚¯ã¯`object_apply_damage`ã§è¡Œã‚ã‚Œã¾ã™ã€‚ã“ã®é–¢æ•°ã¯ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®HPãŒ0ãªã‚‰`1`ã‚’ã€ãã†ã§ãªã„å ´åˆã¯`0`ã‚’è¿”ã—ã¾ã™ã€‚ã“ã®é–¢æ•°ã¯HPãŒ0ã«ãªã£ãŸã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®çˆ†ç™ºå‡¦ç†ã‚‚è¡Œã„ã¾ã™ã€‚

æ¬¡ã«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯ã€ã“ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«å¯¾ã—ã¦è¦ªã‹ã‚‰ã®å‚ç…§ãŒæ®‹ã£ã¦ã„ã‚‹ã‹ã©ã†ã‹ã‚’ãƒã‚§ãƒƒã‚¯ã—ãªã‘ã‚Œã°ãªã‚Šã¾ã›ã‚“ã€‚ã‚‚ã—æ®‹ã£ã¦ã„ãªã‘ã‚Œã°ã€è¦ªã¯æ”»æ’ƒçŠ¶æ…‹ã‚’çµ‚äº†ã—ãŸã“ã¨ã«ãªã‚Šã€ã“ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã¯å¿…è¦ãªããªã‚Šã¾ã™ã€‚

ã“ã®å ´åˆã€updateãƒ«ãƒ¼ãƒãƒ³ã¯ã€ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®ç¾åœ¨ã®çŠ¶æ…‹ã«å¿œã˜ãŸé–¢æ•°ã€idle(`magtect_object_update_idle`), pull(`magtect_object_update_pull`), attack(`magtect_object_update_attack`)ã«ã‚¸ãƒ£ãƒ³ãƒ—ã—ã¾ã™ã€‚


```asm
magtect_object_update_idle:
    push {r4,r5,r6,r7,r14}
    ldrb r0, [r5,0x0A]
    tst r0,r0
    bne .initialized
    mov r0,0x00
    strb r0, [r5,0x10]          ; idle animation
    mov r0,0x04
    strb r0, [r5,0x0A]
.initialized:
    ldr r0, [r5,0x4C]           ; get parent
    ldr r0, [r0,0x58]           ; get parent AI data
    ldrh r1, [r0,0x22]          ; get keys held
    mov r2,GBAKEY_A
    tst r1,r2
    beq .dead
    mov r2, GBAKey_B
    tst r1,r2
    beq .endroutine
    ; B pressed goto pull
    mov r0,0x04
    strb r0, [r5,0x09]
    mov r0,0x00
    strb r0, [r5,0x0A]
    b .endroutine
.dead:
    bl magtect_destroy
.endroutine:
    pop {r4,r5,r6,r7,r15}
```

```asm
magtect_object_update_pull:
    push r4-r7,r14
    ldrb r0, [r5,0x0A]
    tst r0,r0
    bne .initialized
    mov r0,0x01
    strb r0, [r5,0x10]          ; pull animation
    mov r0,0x04
    strb r0, [r5,0x0A]
    mov r0,0xF3
    bl sound_play               ; play wavey sound

.initialized:
    ; create collision object to pull enemies
    bl object_get_front_direction
    ldrb r1, [r5,0x12]
    add r0,r1,r0                ; get panelx in front of magtect
    ldrb r1, [r5,0x13]          ; get panel y
    mov r2, AttackElement_NULL  ; no element
    mov r3,0x00                 ; z = 0

    ; from left to right, collision setup r1, r2, hiteffect, shape
    ldr r4, =0x0405FF08
    mov r6,0x00                 ; no damage
    mov r7,0x04                 ; pull enemy towards magtect
    bl spawn_collision_region

    ldr r0, [r5,0x4C]           ; get parent
    ldr r0, [r0,0x58]           ; get parent AI data
    ldrh r1, [r0,0x22]          ; get keys held
    mov r2, GBAKey_B
    tst r1,r2
    bne .endroutine

    ; B released go to attack phase
    mov r0,0x08
    strb r0, [r5,0x09]
    mov r0,0x00
    strb r0, [r5,0x0A]
.endroutine:
    pop r4-r7,r15
```

```asm
magtect_object_update_attack:
    push r4-r7,r14
    ldrb r0, [r5,0x0A]
    tst r0,r0
    bne .initialized
    mov r0,0x02
    strb r0, [r5,0x10]          ; pull animation
    mov r0,0x04
    strb r0, [r5,0x0A]
    mov r0,0xF
    strh r0, [r5,0x20]          ; timer for this phase

    ; create collision object to pull enemies
    bl object_get_front_direction
    ldrb r1, [r5,0x12]
    add r0,r1,r0                ; get panelx in front of magtect
    ldrb r1, [r5,0x13]          ; get panel y
    mov r2, AttackElement_Elec  ; no element
    mov r3,0x00                 ; z = 0

    ; from left to right, collision setup r1, r2, hiteffect, shape
    ldr r4, =0x04050301         ;
    ldr r6, [r5,0x2C]           ; attack damage
    mov r7,0x03                 ; Flinches and causes invis
    bl spawn_collision_region
    mov r0,0xBA
    bl sound_play               ; play spark sound
.initialized:
    ldrh r0, [r5,0x20]
    sub r0,0x01
    strh r0, [r5,0x20]
    bgt .endroutine

    ; go back to idle phase after time expires
    mov r0,0x00
    strb r0, [r5,0x09]
    strb r0, [r5,0x0A]
.endroutine:
    pop r4-r7,r15
```

ã™ã¹ã¦ã®ãƒ«ãƒ¼ãƒãƒ³ã¯ã€è¦ªã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆæ§‹é€ ä½“ã‚’å–å¾—ã—ã€æ¬¡ã«ãã®AIæ§‹é€ ä½“ã‚’å–å¾—ã—ã¾ã™ã€‚ãã—ã¦ã€ã“ã®æ§‹é€ ä½“ã‚’ä½¿ã£ã¦ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ã®å…¥åŠ›ã‚’å–å¾—ã—ã¾ã™ã€‚

The Pull and Attack states rely on collision regions. This is a separate object the game commonly uses to create collisions in a timing sensitive manner.

Pull uses a collision region that's in the shape of a 6x1 line that has the pull property is non-flinching to create the magnet pull effect.

Attack then uses another collision region to attack the panel in front of the magtect without having to modify the object's collision region.

You can look at the attached full code to see how the objects/attacks were added to the game.

## å‚è€ƒè¨˜äº‹

- [BN6 Battle Objects Data](https://forums.therockmanexezone.com/bn6-battle-objects-data-t5346.html)
- [BN5/BN6 Battle Objects](https://forums.therockmanexezone.com/viewtopic.php?p=344491#p344491)
